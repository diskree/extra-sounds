plugins {
    id "fabric-loom"
    id "maven-publish"
    id "com.matthewprenger.cursegradle"
    id "com.modrinth.minotaur"
    id "org.jetbrains.changelog"
}

var targetJdk = 17

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

var versionTitle = "$project.mod_version for $project.minecraft_version"

tasks.runClient.dependsOn(tasks.build)

repositories {
    maven {
        url = uri("https://api.modrinth.com/maven/")
    }
    maven {
        name = "stashymane's repo"
        url = "https://repo.stashy.dev/releases"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
    modImplementation include("maven.modrinth:arrp:${project.arrp_version}")
    compileOnly include(project(path: ':sound-categories'))
    runtimeOnly fileTree(include: ["*+${project.minecraft_version}-dev.jar"], exclude: ['*-sources.jar'], dir: 'sound-categories/build/devlibs')
    modCompileOnly 'maven.modrinth:rei:12.0.645+fabric'
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(targetJdk)
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.toVersion(targetJdk)
    targetCompatibility = JavaVersion.toVersion(targetJdk)
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        maven {
            url = "https://repo.stashy.dev"
            credentials {
                username = project.hasProperty("publish_username") ? project.publish_username : ""
                password = project.hasProperty("publish_password") ? project.publish_password : ""
            }
        }
    }
}

curseforge {
    apiKey = System.getenv("CURSE_API_KEY") ?: ""
    project {
        id = "478890"
        changelog = project.changelog.get(project.mod_version).toHTML()
        changelogType = "html"
        releaseType = "release"
        addGameVersion project.minecraft_version

        mainArtifact(remapJar) {
            displayName = versionTitle

            relations {
                requiredDependency "fabric-api"
                embeddedLibrary "arrp"
                embeddedLibrary "sound-categories"
            }
        }
    }

    options {
        forgeGradleIntegration = false
        detectNewerJava = true
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN") ?: ""
    changelog = project.changelog.get(project.mod_version).toText()
    projectId = "uPWCQrVL"
    versionNumber = version
    versionName = versionTitle
    uploadFile = remapJar
    gameVersions = [project.minecraft_version]
    loaders = ["fabric"]
}

task publishMod() {
    dependsOn(tasks.modrinth)
    dependsOn(tasks.curseforge)
}